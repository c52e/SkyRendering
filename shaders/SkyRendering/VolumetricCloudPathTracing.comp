#include "VolumetricCloudCommon.glsl"
#include "VolumetricCloudShadowInterface.glsl"
#include "../Base/Noise.glsl"

layout(local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y) in;

layout(binding = 0) uniform sampler2D transmittance_texture;
layout(binding = 1) uniform sampler3D aerial_perspective_luminance_texture;
layout(binding = 2) uniform sampler3D aerial_perspective_transmittance_texture;
layout(binding = 3) uniform sampler3D shadow_froxel;
layout(binding = 4) uniform samplerCube environment_luminance_texture;

layout(binding = 0, rgba32f) uniform image2D accumulating_image;
layout(binding = 1, rgba16f) uniform image2D display_image;

layout(location = 0) uniform uint kFrameId;

const vec2 kCloudAABBCenter = vec2(0.0);
const vec3 kCloudAABBMin = vec3(kCloudAABBCenter - vec2(kCloudHalfWidth), uBottomAltitude);
const vec3 kCloudAABBMax = vec3(kCloudAABBCenter + vec2(kCloudHalfWidth), uTopAltitude);

// https://github.com/sebh/UnrealEngineSkyAtmosphere
// https://graphics.pixar.com/library/ProductionVolumeRendering/

struct Ray {
    vec3 o;
    vec3 d;
};

Ray CreateRay(vec3 o, vec3 d) {
    Ray r;
    r.o = o;
    r.d = d;
    return r;
}

struct Context {
    uint seed;
    Ray ray;
    float sigma_t_max;
};

float Random01(inout Context ctx) {
    float res = float(ctx.seed) / (4294967296.0);
    ctx.seed = PRNG(ctx.seed);
    return res;
}

vec3 UniformSphereSample(inout Context ctx) {
    float phi = 2.0 * PI * Random01(ctx);
    float cos_theta = 1.0 - 2.0 * Random01(ctx);
    float sin_theta = sqrt(clamp(1.0 - cos_theta * cos_theta, 0, 1));
    return vec3(cos(phi) * sin_theta, sin(phi) * sin_theta  , cos_theta);
}

vec2 CloudRegionIntersect(Ray ray) {
    vec2 t = vec2(0, 1e7);
    for (int i = 0; i < 3; ++i) {
//        if (abs(ray.d[i]) < 1e-7) {
//            if (ray.o[i] < kCloudAABBMin[i] || ray.o[i] > kCloudAABBMax[i])
//                return vec2(0);
//            else
//                continue;
//        }
        float t1 = (kCloudAABBMin[i] - ray.o[i]) / ray.d[i];
        float t2 = (kCloudAABBMax[i] - ray.o[i]) / ray.d[i];
        float tmin = min(t1, t2);
        float tmax = max(t1, t2);
        t.x = max(t.x, tmin);
        t.y = min(t.y, tmax);
    }

    return t;
}

bool IsInsideCloudRegion(vec3 p) {
    const float kMargin = 0.01; // More robust
    return all(clamp(p, kCloudAABBMin - kMargin, kCloudAABBMax + kMargin) == p);
}

float InfiniteTransmittanceIS(float sigma_t, float zeta) {
    return -log(1.0 - zeta) / sigma_t;
}

// Should be defined in material shader
float SampleSigmaT(vec3 pos, float height01);

float SampleSigmaT(vec3 P) {
    return SampleSigmaT(P, clamp((P.z - uBottomAltitude) / (uTopAltitude - uBottomAltitude), 0, 1));
}

float GetPhase(float cos_theta) {
    return mix(HenyeyGreenstein(cos_theta, kBackPhaseG),
                HenyeyGreenstein(cos_theta, kForwardPhaseG), kForwardScatteringRatio);
}

void CreateOrthonormalBasis(vec3 N, out vec3 t0, out vec3 t1) {
#if 0
	const vec3 up = vec3(0.0, 1.0, 0.0);
	const vec3 right = vec3(1.0, 0.0, 0.0);
	if (abs(dot(N, up)) > 0.01) {
		t0 = normalize(cross(N, right));
		t1 = normalize(cross(N, t0));
	} else {
		t0 = normalize(cross(N, up));
		t1 = normalize(cross(N, t0));
	}
#else
    // https://graphics.pixar.com/library/OrthonormalB/paper.pdf
    float s = (N.z >= 0.0 ? 1.0 : -1.0);
    float a = -1.0 / (s + N.z);
    float b = N.x * N.y * a;
    t0 = vec3(1.0 + s * N.x * N.x * a, s * b, -s * N.x);
    t1 = vec3(b, s + N.y * N.y * a, -N.y);
#endif
}

void GenerateHGSample(inout Context ctx, out vec3 direction, out float value_over_pdf) {
#if IMPORTANCE_SAMPLING
    float g = Random01(ctx) < kForwardScatteringRatio ? kForwardPhaseG : kBackPhaseG;
    float cos_theta = HenyeyGreensteinInvertcdf(Random01(ctx), g);
    float sin_theta = sqrt(clamp(1.0 - cos_theta * cos_theta, 0, 1));
    vec3 t0, t1;
    CreateOrthonormalBasis(ctx.ray.d, t0, t1);
    float phi = 2.0 * PI * Random01(ctx);
    direction = vec3(sin_theta * sin(phi) * t0 + sin_theta * cos(phi) * t1 + cos_theta * ctx.ray.d);

//    value = GetPhase(dot(ctx.ray.d, direction));
//    pdf = value;
    value_over_pdf = 1.0;
#else
    direction = UniformSphereSample(ctx);
    float value = GetPhase(dot(ctx.ray.d, direction));
    float pdf = 1.0 / (4.0 * PI);
    value_over_pdf = value / pdf;
#endif
}

void GenerateLambertSample(inout Context ctx, vec3 N, vec3 albedo, out vec3 direction, out vec3 value_over_pdf) {
    float sin_theta = sqrt(Random01(ctx));
    float cos_theta = sqrt(clamp(1.0 - sin_theta * sin_theta, 0, 1));
    vec3 t0, t1;
    CreateOrthonormalBasis(N, t0, t1);
    float phi = 2.0 * PI * Random01(ctx);
    direction = vec3(sin_theta * sin(phi) * t0 + sin_theta * cos(phi) * t1 + cos_theta * N);
//    value = albedo * INV_PI * cos_theta;
//    pdf = INV_PI * cos_theta;
    value_over_pdf = albedo;
}

vec3 GetSunIlluminance(vec3 pos) {
    return GetSunVisibility(transmittance_texture, pos) * solar_illuminance;
}

float TransmittanceEstimation(Context ctx, Ray ray) {
    float transmittance = 1.0;
    vec2 inter_t = CloudRegionIntersect(ray);
    if (inter_t.x >= inter_t.y)
        return transmittance;
    
    float t = inter_t.x;
    while (true) {
		t += InfiniteTransmittanceIS(ctx.sigma_t_max, Random01(ctx));
        if (t > inter_t.y)
            break;

        float sigma_t = SampleSigmaT(ray.o + ray.d * t);
        transmittance *= 1.0 - max(0, sigma_t / ctx.sigma_t_max); 
    }
    return clamp(transmittance, 0, 1);
}

vec3 SampleLuminanceFromLight(inout Context ctx, vec3 pos, vec3 bsdf_with_cosine) {
    const float kSunSolidAngle = 1.0; // This should be an infinitesimal, but has no effect on the final result
    vec3 light_luminance = GetSunIlluminance(pos) / kSunSolidAngle;
    float pdf = 1.0 / kSunSolidAngle;
    return TransmittanceEstimation(ctx, CreateRay(pos, uSunDirection)) * light_luminance * bsdf_with_cosine / pdf;
}

vec4 Trace(inout Context ctx, vec3 view_dir, out bool has_scattered, out float scattered_t) {
    // Should tracing atmosphere for better result
    vec3 L = vec3(0.0);
    vec3 throughput = vec3(1.0);
    has_scattered = false;
    ctx.ray = CreateRay(uCameraPos, view_dir);
    vec2 camera_inter_t = CloudRegionIntersect(ctx.ray);
    if (camera_inter_t.x >= camera_inter_t.y)
        return vec4(L, throughput);

    ctx.ray.o += camera_inter_t.x * ctx.ray.d;
    int istep = 0;
    while (istep < kMaxBounces && max(throughput.r, max(throughput.g, throughput.b)) > 0.0) {
//        if (!IsInsideCloudRegion(ctx.ray.o))
//            break;
        
        vec2 inter_t = CloudRegionIntersect(ctx.ray);
        if (inter_t.x >= inter_t.y)
            break;
        float t_max = inter_t.y;
        float t = inter_t.x;
        bool event_scatter = false;
        while (true) {
            if (ctx.sigma_t_max <= 0) break;

            t += InfiniteTransmittanceIS(ctx.sigma_t_max, Random01(ctx));
            if (t > t_max) break;

            vec3 P = ctx.ray.o + ctx.ray.d * t;
            float sigma_t = SampleSigmaT(P);
            
		    float xi = Random01(ctx);
            if (xi < sigma_t / ctx.sigma_t_max) {
                event_scatter = true;
                break;
            }
        }
        
        if (!event_scatter) {
#ifdef ENVIRONMENT_LIGHT_OFF
            break;
#endif
            if (!has_scattered) 
                break;

#ifdef ENVIRONMENT_LIGHT_CONST_ENVIRONMENT_MAP
            L += throughput * texture(environment_luminance_texture, kModelMatrix3 * ctx.ray.d).rgb;
            break;
#endif

	        vec3 up_dir = vec3(ctx.ray.o.xy, ctx.ray.o.z + uEarthRadius);
	        float r = length(up_dir);
	        up_dir /= r;
	        float mu = dot(ctx.ray.d, up_dir);
            if (!RayIntersectsGround(r, mu)) {
                L += throughput * texture(environment_luminance_texture, kModelMatrix3 * ctx.ray.d).rgb;
                break;
            }
            ctx.ray.o += ctx.ray.d * DistanceToBottomAtmosphereBoundary(r, mu);
            vec3 ground_normal = normalize(vec3(ctx.ray.o.xy, ctx.ray.o.z + uEarthRadius));
            vec3 light_bsdf = INV_PI * ground_albedo;
            float NdotL = dot(ground_normal, uSunDirection);
            L += throughput * SampleLuminanceFromLight(ctx, ctx.ray.o, light_bsdf * NdotL);

#ifdef ENVIRONMENT_LIGHT_GROUND_SINGLE_BOUNCE
            break;
#endif

            vec3 bsdf_with_cosine_over_pdf;
            GenerateLambertSample(ctx, ground_normal, ground_albedo, ctx.ray.d, bsdf_with_cosine_over_pdf);
            throughput *= bsdf_with_cosine_over_pdf;
        } else {
            if (!has_scattered)
                scattered_t = distance(uCameraPos, ctx.ray.o);
            has_scattered = true;

            ctx.ray.o += ctx.ray.d * t;
            float light_bsdf = GetPhase(dot(ctx.ray.d, uSunDirection));
            L += throughput * SampleLuminanceFromLight(ctx, ctx.ray.o, vec3(light_bsdf));
            
            float bsdf_over_pdf;
            GenerateHGSample(ctx, ctx.ray.d, bsdf_over_pdf);
            throughput *= bsdf_over_pdf;
        }
        
        ++istep;
    }

    return vec4(L, has_scattered ? 0.0 : 1.0);
}

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    Context ctx;
    ctx.seed = PRNG(PRNG(PRNG(gl_GlobalInvocationID.x) + gl_GlobalInvocationID.y) + kFrameId);
    ctx.sigma_t_max = kSigmaTMax;

    vec2 uv = (vec2(pos) + 0.5) / vec2(imageSize(display_image));
    vec3 frag_pos = ProjectiveMul(uInvMVP, vec3(uv, 1.0) * 2.0 - 1.0);
    vec3 view_dir = normalize(frag_pos - uCameraPos);

    bool has_scattered;
    float scattered_t;
    vec4 this_res = Trace(ctx, view_dir, has_scattered, scattered_t);
    if (has_scattered) {
        // Apply atmosphere scattering
        float r = uCameraPos.z + uEarthRadius;
        float mu = view_dir.z;
        vec3 atmosphere_transmittance;
        vec3 atmosphere_luminance = GetAerialPerspective(aerial_perspective_luminance_texture,
            aerial_perspective_transmittance_texture, uv, scattered_t, r, mu, atmosphere_transmittance);
        atmosphere_luminance *= SampleRayScatterVisibility(shadow_froxel, uv, scattered_t, uInvShadowFroxelMaxDistance);
        this_res.rgb = this_res.rgb * atmosphere_transmittance + atmosphere_luminance;
    }

    vec4 accumulated = imageLoad(accumulating_image, pos) + this_res;
    imageStore(accumulating_image, pos, vec4(accumulated));

    vec4 color = imageLoad(display_image, pos);
    vec4 avg_res = accumulated / float(kFrameId);
    
    color.rgb = color.rgb * avg_res.a + avg_res.rgb;
    imageStore(display_image, pos, color);
}
